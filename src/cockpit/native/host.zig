const std = @import("std");
const native_sdk = @import("native_sdk");
const support = @import("../phux_support.zig");
const provider_contract = @import("provider_contract");
const model_module = @import("../model.zig");
const layout = @import("../layout.zig");
const app_types = @import("../app_types.zig");
const pointer_input = @import("../pointer_input.zig");
const update_module = @import("../update.zig");
const scene_module = @import("scene.zig");

const canvas = native_sdk.canvas;
const geometry = native_sdk.geometry;
const Model = model_module.Model;
const Msg = app_types.Msg;
const TerminalApp = app_types.TerminalApp;
const LocalTerminalId = support.LocalTerminalId;
const TerminalRef = support.TerminalRef;
const canvas_label = scene_module.canvas_label;
const webview_label = scene_module.webview_label;
const localRef = support.localRef;
const selection_autoscroll_timer_id = app_types.selection_autoscroll_timer_id;
const selection_autoscroll_interval_ns: u64 = 15 * std.time.ns_per_ms;
const appShortcutKeyMask = update_module.appShortcutKeyMask;
const commandShortcutKeyMask = update_module.commandShortcutKeyMask;
const pointerCaptureFor = pointer_input.pointerCaptureFor;
const modelHasSelectionAutoscroll = pointer_input.modelHasSelectionAutoscroll;
/// UiApp owns the deterministic model/view/effects loop. This host adds the
/// one imperative operation native surface switching requires: moving the OS
/// first responder after a global tab shortcut. Without it, a parked WebKit
/// view can keep consuming text after the model has returned to a terminal.
pub const CockpitHost = struct {
    inner: TerminalApp = undefined,
    inner_app: native_sdk.App = undefined,
    /// Global AppKit shortcuts and canvas key delivery can describe the same
    /// physical edge. Suppress only the duplicate canvas edge; the model's
    /// latch remains reserved for shortcuts originating on the canvas itself.
    suppressed_canvas_shortcuts: u64 = 0,
    /// Platform shortcut callbacks have no key phase. Hold the physical key
    /// until a canvas release or a different shortcut edge so repeated
    /// callbacks for one edge execute the command exactly once.
    global_shortcut_keys_held: u64 = 0,
    selection_autoscroll_timer_active: bool = false,

    pub fn init(self: *CockpitHost, allocator: std.mem.Allocator, model: Model, options: TerminalApp.Options) void {
        var host_options = options;
        // Production wheel input is routed by the retained terminal overlay
        // below; disable the app-global geometry fallback to avoid a duplicate.
        host_options.on_wheel = null;
        self.inner = TerminalApp.init(allocator, model, host_options);
        self.inner_app = self.inner.app();
        self.suppressed_canvas_shortcuts = 0;
        self.global_shortcut_keys_held = 0;
        self.selection_autoscroll_timer_active = false;
    }

    pub fn deinit(self: *CockpitHost) void {
        self.inner.deinit();
    }

    pub fn app(self: *CockpitHost) native_sdk.App {
        return .{
            .context = self,
            .name = self.inner_app.name,
            .source = self.inner_app.source,
            .source_fn = if (self.inner_app.source_fn != null) source else null,
            .scene_fn = if (self.inner_app.scene_fn != null) scene else null,
            .start_fn = if (self.inner_app.start_fn != null) start else null,
            .event_fn = if (self.inner_app.event_fn != null) event else null,
            .stop_fn = if (self.inner_app.stop_fn != null) stop else null,
            .replay_fn = if (self.inner_app.replay_fn != null) replay else null,
        };
    }

    fn source(context: *anyopaque) anyerror!native_sdk.platform.WebViewSource {
        const self: *CockpitHost = @ptrCast(@alignCast(context));
        return self.inner_app.webViewSource();
    }

    fn scene(context: *anyopaque) anyerror!native_sdk.ShellConfig {
        const self: *CockpitHost = @ptrCast(@alignCast(context));
        return (try self.inner_app.scene()) orelse return error.SceneUnavailable;
    }

    fn start(context: *anyopaque, runtime: *native_sdk.Runtime) anyerror!void {
        const self: *CockpitHost = @ptrCast(@alignCast(context));
        try self.inner_app.start(runtime);
    }

    fn event(context: *anyopaque, runtime: *native_sdk.Runtime, event_value: native_sdk.Event) anyerror!void {
        const self: *CockpitHost = @ptrCast(@alignCast(context));
        defer self.syncSelectionAutoscrollTimer(runtime) catch {};
        switch (event_value) {
            .command => |command| {
                if (command.source == .shortcut) {
                    const mask = commandShortcutKeyMask(command.name);
                    if (mask != 0 and
                        ((self.inner.model.consumed_shortcut_keys_held & mask) != 0 or
                            (self.global_shortcut_keys_held & mask) != 0)) return;
                    // Preserve a still-held older shortcut until its canvas
                    // release arrives. A new global edge for this key clears
                    // stale duplicate suppression left by WebKit focus.
                    self.suppressed_canvas_shortcuts &= ~mask;
                    self.suppressed_canvas_shortcuts |= self.global_shortcut_keys_held;
                    self.global_shortcut_keys_held = mask;
                }
            },
            .gpu_surface_input => |input| {
                const mask = appShortcutKeyMask(input.key);
                const global_owned = mask != 0 and (self.global_shortcut_keys_held & mask) != 0;
                if (input.kind == .key_up) self.global_shortcut_keys_held &= ~mask;
                if (global_owned) {
                    if (input.kind == .key_up) self.suppressed_canvas_shortcuts &= ~mask;
                    return;
                }
                if (mask != 0 and (self.suppressed_canvas_shortcuts & mask) != 0) {
                    if (input.kind == .key_up) self.suppressed_canvas_shortcuts &= ~mask;
                    return;
                }
            },
            .canvas_widget_pointer => |pointer_event| {
                if (try self.routeTerminalPointer(runtime, pointer_event)) return;
            },
            .shortcut => |shortcut| {
                const mask = appShortcutKeyMask(shortcut.key);
                if (mask != 0 and (self.inner.model.consumed_shortcut_keys_held & mask) != 0) {
                    // Canvas handled key-down first. Do not execute the global
                    // copy, but own its eventual canvas release after focus.
                    if (self.inner.model.selectedTerminalRef() == null) {
                        self.inner.model.consumed_shortcut_keys_held &= ~mask;
                    } else {
                        self.suppressed_canvas_shortcuts |= mask;
                    }
                    try self.focusSelectedContent(runtime, shortcut.window_id);
                    return;
                }
                if (mask != 0 and (self.global_shortcut_keys_held & mask) != 0) {
                    try self.focusSelectedContent(runtime, shortcut.window_id);
                    return;
                }
            },
            .lifecycle => |lifecycle| if (lifecycle == .deactivate) {
                self.suppressed_canvas_shortcuts = 0;
                self.global_shortcut_keys_held = 0;
            },
            else => {},
        }
        const selected_before = self.inner.model.selectedSurface();
        try self.inner_app.event(runtime, event_value);
        if (!selected_before.eql(self.inner.model.selectedSurface())) {
            const window_id = switch (event_value) {
                .shortcut => |shortcut| shortcut.window_id,
                .gpu_surface_input => |input| input.window_id,
                else => 1,
            };
            try self.focusSelectedContent(runtime, window_id);
            return;
        }
        switch (event_value) {
            .shortcut => |shortcut| {
                if (!std.mem.startsWith(u8, shortcut.id, "tab.") and
                    !std.mem.startsWith(u8, shortcut.id, "layout.") and
                    !std.mem.startsWith(u8, shortcut.id, "pane.")) return;
                try self.focusSelectedContent(runtime, shortcut.window_id);
                if (self.inner.model.selectedTerminalRef() != null) {
                    self.suppressed_canvas_shortcuts |= appShortcutKeyMask(shortcut.key);
                }
            },
            .gpu_surface_input => |input| {
                if (input.kind != .pointer_up or !std.mem.eql(u8, input.label, canvas_label)) return;
                if (canvasActionControlFocused(runtime, input.window_id)) {
                    try self.focusSelectedContent(runtime, input.window_id);
                }
            },
            else => {},
        }
    }

    fn focusSelectedContent(self: *CockpitHost, runtime: *native_sdk.Runtime, window_id: native_sdk.platform.WindowId) !void {
        const target = if (self.inner.model.selectedTerminalRef() == null) webview_label else canvas_label;
        try runtime.focusView(window_id, target);
        if (std.mem.eql(u8, target, canvas_label)) clearCanvasWidgetFocus(runtime, window_id);
    }

    fn canvasActionControlFocused(runtime: *native_sdk.Runtime, window_id: native_sdk.platform.WindowId) bool {
        for (runtime.views[0..runtime.view_count]) |*runtime_view| {
            if (runtime_view.window_id != window_id or !std.mem.eql(u8, runtime_view.label, canvas_label)) continue;
            const focused = runtime_view.canvas_widget_focused_id;
            if (focused == 0) return false;
            const node = runtime_view.widgetLayoutTree().findById(focused) orelse return false;
            // Role, not just kind: a tab trigger is a plain container now
            // (a segmented control cannot host a close affordance), and a
            // click that leaves canvas widget focus parked on it is a click
            // after which the keyboard no longer reaches the terminal.
            if (node.widget.semantics.role == .tab) return true;
            return node.widget.kind == .segmented_control or
                node.widget.kind == .toggle_button or
                node.widget.kind == .button;
        }
        return false;
    }

    fn clearCanvasWidgetFocus(runtime: *native_sdk.Runtime, window_id: native_sdk.platform.WindowId) void {
        for (runtime.views[0..runtime.view_count]) |*runtime_view| {
            if (runtime_view.window_id != window_id or !std.mem.eql(u8, runtime_view.label, canvas_label)) continue;
            runtime_view.canvas_widget_focused_id = 0;
            runtime_view.canvas_widget_focus_visible_id = 0;
            runtime_view.canvas_widget_focus_visible_keyboard = false;
            return;
        }
    }

    /// Narrow v0.7.1 adapter: consume the SDK's already-routed/captured widget
    /// event exactly once. Custom widgets cannot bind a continuous pointer Msg,
    /// and the SDK keeps only one pressed-widget register per canvas, so model
    /// capture below supplies the missing per-window/per-pointer ownership.
    fn routeTerminalPointer(
        self: *CockpitHost,
        runtime: *native_sdk.Runtime,
        routed: native_sdk.runtime.CanvasWidgetPointerEvent,
    ) !bool {
        if (!std.mem.eql(u8, routed.view_label, canvas_label)) return false;
        const pointer = routed.pointer;
        // A new down supersedes only this physical pointer's old capture,
        // even when the new target is chrome rather than another terminal.
        if (pointer.phase == .down) {
            if (pointerCaptureFor(&self.inner.model, routed.window_id, pointer.pointer_id)) |previous| {
                try self.inner.dispatch(runtime, routed.window_id, .{ .pointer = .{
                    .window_id = previous.window_id,
                    .terminal_id = previous.terminal_id,
                    .generation = previous.generation,
                    .phase = .cancel,
                    .pointer_id = previous.pointer_id,
                    .button = previous.button,
                    .point = previous.last_point,
                    .frame = previous.frame,
                    .modifiers = previous.modifiers,
                } });
            }
        }
        const capture = switch (pointer.phase) {
            .move, .up, .cancel => pointerCaptureFor(&self.inner.model, routed.window_id, pointer.pointer_id),
            .hover, .down, .wheel => null,
        };

        var terminal_id: LocalTerminalId = undefined;
        var generation: u64 = 0;
        var frame: geometry.RectF = .{};
        if (capture) |owned| {
            terminal_id = owned.terminal_id;
            generation = owned.generation;
            frame = if (routed.target) |target|
                if (target.id == terminalInteractionWidgetId(owned.terminal_id)) target.bounds else terminalInteractionFrame(runtime, routed.window_id, owned.terminal_id) orelse owned.frame
            else
                terminalInteractionFrame(runtime, routed.window_id, owned.terminal_id) orelse owned.frame;
        } else {
            if (pointer.phase == .up or pointer.phase == .cancel or pointer.phase == .move) {
                // A stale/unrelated terminal edge cannot borrow the SDK's
                // canvas-global pressed id and disturb another pointer.
                if (routed.target) |target| {
                    if (terminalIdForInteractionWidget(&self.inner.model, target.id) != null) return true;
                }
                return false;
            }
            const target = routed.target orelse return false;
            terminal_id = terminalIdForInteractionWidget(&self.inner.model, target.id) orelse return false;
            const pane = self.inner.model.provider.terminal(localRef(terminal_id)) orelse return true;
            generation = pane.session_generation;
            frame = target.bounds;
        }

        try self.inner.dispatch(runtime, routed.window_id, .{ .pointer = .{
            .window_id = routed.window_id,
            .terminal_id = terminal_id,
            .generation = generation,
            .phase = pointer.phase,
            .pointer_id = pointer.pointer_id,
            .button = pointer.button,
            .click_count = pointer.click_count,
            .point = pointer.point,
            .frame = frame,
            .delta = pointer.delta,
            .modifiers = .{
                .shift = pointer.modifiers.shift,
                .control = pointer.modifiers.control,
                .alt = pointer.modifiers.alt,
                .super = pointer.modifiers.super,
            },
        } });
        if (pointer.phase == .down) try self.focusSelectedContent(runtime, routed.window_id);
        return true;
    }

    fn syncSelectionAutoscrollTimer(self: *CockpitHost, runtime: *native_sdk.Runtime) !void {
        const needed = modelHasSelectionAutoscroll(&self.inner.model);
        if (needed == self.selection_autoscroll_timer_active) return;
        if (needed) {
            try runtime.startTimer(selection_autoscroll_timer_id, selection_autoscroll_interval_ns, true);
        } else {
            try runtime.cancelTimer(selection_autoscroll_timer_id);
        }
        self.selection_autoscroll_timer_active = needed;
    }

    fn stop(context: *anyopaque, runtime: *native_sdk.Runtime) anyerror!void {
        const self: *CockpitHost = @ptrCast(@alignCast(context));
        if (self.selection_autoscroll_timer_active) {
            runtime.cancelTimer(selection_autoscroll_timer_id) catch {};
            self.selection_autoscroll_timer_active = false;
        }
        try self.inner_app.stop(runtime);
    }

    fn replay(context: *anyopaque, control: native_sdk.runtime.ReplayControl) anyerror!void {
        const self: *CockpitHost = @ptrCast(@alignCast(context));
        try self.inner_app.replayControl(control);
    }
};

fn terminalInteractionWidgetId(id: LocalTerminalId) canvas.ObjectId {
    return canvas.globalWidgetId(.terminal, .{ .index = @intCast(@intFromEnum(id)) });
}

/// Routed widget pointer input reaches LOCAL terminals only: the remote path
/// runs through the NSEvent monitor, which carries its own hit testing.
fn terminalIdForInteractionWidget(model: *const Model, widget_id: canvas.ObjectId) ?LocalTerminalId {
    // Every pane of every tab, not just one tab's: a widget id survives a tab
    // switch, and routing must resolve it against the whole workspace.
    for (0..model.tab_count) |tab_index| {
        const tree = model.treeConst(tab_index) orelse continue;
        var refs: [layout.max_panes]TerminalRef = undefined;
        const count = tree.terminals(&refs);
        for (refs[0..count]) |id| {
            const local = provider_contract.localId(id) orelse continue;
            if (terminalInteractionWidgetId(local) == widget_id) return local;
        }
    }
    return null;
}

fn terminalInteractionFrame(
    runtime: *native_sdk.Runtime,
    window_id: native_sdk.platform.WindowId,
    id: LocalTerminalId,
) ?geometry.RectF {
    const widget_id = terminalInteractionWidgetId(id);
    for (runtime.views[0..runtime.view_count]) |*runtime_view| {
        if (runtime_view.window_id != window_id or !std.mem.eql(u8, runtime_view.label, canvas_label)) continue;
        const node = runtime_view.widgetLayoutTree().findById(widget_id) orelse return null;
        if (node.widget.kind != .terminal) return null;
        return node.frame;
    }
    return null;
}
